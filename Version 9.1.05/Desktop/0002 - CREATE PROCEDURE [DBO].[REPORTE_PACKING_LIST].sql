
/****** Object:  StoredProcedure [dbo].[reporte_packing_list]    Script Date: 03/11/2014 14:56:59 ******/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[reporte_packing_list]') AND type in (N'P', N'PC'))
DROP PROCEDURE [dbo].[reporte_packing_list]
GO


/****** Object:  StoredProcedure [dbo].[reporte_packing_list]    Script Date: 03/11/2014 14:56:59 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


-- =============================================
-- Author:		LRojas
-- Create date: 25/04/2012
-- Description:	Procedimiento para Reporte Packing List
-- =============================================
CREATE PROCEDURE [DBO].[REPORTE_PACKING_LIST] 
	@CLIENTE_ID as varchar(15) OUTPUT,
    @PEDIDO_ID as varchar(100) OUTPUT
AS
BEGIN
	SET NOCOUNT ON;
	
	SELECT	C.RAZON_SOCIAL [EMPRESA], 
			LTRIM(RTRIM(ISNULL(D.CPTE_PREFIJO, '') + ' ' + ISNULL(D.CPTE_NUMERO, ''))) AS [REMITO], 
			D.NRO_REMITO AS [PEDIDO], 
			S.NOMBRE AS [DESTINATARIO], 
			DIR.DIRECCION,
			CASE WHEN P.PALLET_CONTROLADO = '0' THEN 0 ELSE P.PALLET_PICKING END [NRO_DE_BULTO],
			P.PRODUCTO_ID,
			PRO.DESCRIPCION,
			SUM(P.CANT_CONFIRMADA) [UNIDADES_POR_CAJA],
			CP.TOTAL_BULTOS [TOTAL_DE_BULTOS],			
			P.NRO_LOTE AS [NRO_LOTE],
			P.NRO_PARTIDA AS [NRO_PARTIDA],
			(ISNULL(PRO.PESO,0) * P.CANT_CONFIRMADA) AS [PESO],
			((ISNULL(PRO.ALTO ,0) * ISNULL(PRO.ANCHO ,0) * ISNULL(PRO.LARGO ,0) ) * P.CANT_CONFIRMADA) AS [VOLUMEN],
			P.NRO_SERIE AS [NRO_SERIE],
			DD.NRO_DESPACHO AS [NRO_DESPACHO] ,
			DD.CAT_LOG_ID AS [CAT_LOG_ID],
			DD.NRO_BULTO AS [NRO_BULTO],
			DD.FECHA_VENCIMIENTO AS [FECHA_VENCIMIENTO]
	  FROM	DOCUMENTO D INNER JOIN DET_DOCUMENTO DD ON(D.DOCUMENTO_ID = DD.DOCUMENTO_ID AND D.CLIENTE_ID = DD.CLIENTE_ID) 
			INNER JOIN PICKING P ON(P.DOCUMENTO_ID = DD.DOCUMENTO_ID AND P.NRO_LINEA = DD.NRO_LINEA AND P.CLIENTE_ID = DD.CLIENTE_ID) 
			INNER JOIN SUCURSAL S ON(S.SUCURSAL_ID = D.SUCURSAL_DESTINO AND S.CLIENTE_ID = P.CLIENTE_ID) 
			INNER JOIN SYS_INT_DOCUMENTO ID ON (ID.CLIENTE_ID = P.CLIENTE_ID AND ID.DOC_EXT = D.NRO_REMITO) -- 
			INNER JOIN CLIENTE C ON (C.CLIENTE_ID = P.CLIENTE_ID)
			INNER JOIN PRODUCTO PRO ON (PRO.CLIENTE_ID = P.CLIENTE_ID AND PRO.PRODUCTO_ID = P.PRODUCTO_ID)
			INNER JOIN (Select  S.SUCURSAL_ID,
								S.CLIENTE_ID,
								S.CALLE + 
								CASE WHEN S.NUMERO IS NOT NULL THEN ' ' + S.NUMERO ELSE '' END +
								CASE WHEN S.LOCALIDAD IS NOT NULL THEN ', ' + S.LOCALIDAD ELSE '' END +
								CASE WHEN S.PROVINCIA_ID IS NOT NULL THEN ', ' + PR.DESCRIPCION ELSE '' END +
								CASE WHEN S.PAIS_ID IS NOT NULL THEN ', ' + P.DESCRIPCION ELSE '' END 
								[DIRECCION]
						From	SUCURSAL S 
								LEFT JOIN PROVINCIA PR ON (PR.PAIS_ID = S.PAIS_ID AND PR.PROVINCIA_ID = S.PROVINCIA_ID) 
								LEFT JOIN PAIS P ON (P.PAIS_ID = S.PAIS_ID AND PR.PAIS_ID = S.PAIS_ID)
			) DIR ON(DIR.SUCURSAL_ID = S.SUCURSAL_ID AND DIR.CLIENTE_ID = S.CLIENTE_ID) 
			INNER JOIN (SELECT	DOCUMENTO_ID, COUNT(DISTINCT PALLET_PICKING) AS TOTAL_BULTOS 
						FROM	PICKING 
						WHERE	PALLET_CONTROLADO = '1' GROUP BY DOCUMENTO_ID) CP ON (CP.DOCUMENTO_ID = P.DOCUMENTO_ID)
	 WHERE	D.TIPO_OPERACION_ID = 'EGR' 
			AND D.STATUS = 'D30' 
			AND D.NRO_REMITO IS NOT NULL 
			AND P.FACTURADO = '0' 
			AND P.ST_CAMION = '0' 
		    AND P.CLIENTE_ID = @CLIENTE_ID
		    AND D.NRO_REMITO = @PEDIDO_ID 
	 GROUP BY 
			C.RAZON_SOCIAL, 
			LTRIM(RTRIM(ISNULL(D.CPTE_PREFIJO, '') + ' ' + ISNULL(D.CPTE_NUMERO, ''))), 
			D.NRO_REMITO, 
			S.NOMBRE, 
			DIR.DIRECCION,
			CASE WHEN P.PALLET_CONTROLADO = '0' THEN 0 ELSE P.PALLET_PICKING END,
			P.PRODUCTO_ID,
			PRO.DESCRIPCION, 
			CP.TOTAL_BULTOS,
			P.NRO_LOTE,
			P.NRO_PARTIDA,
			DD.PESO,
			(ISNULL(PRO.PESO,0) * P.CANT_CONFIRMADA),
			((ISNULL(PRO.ALTO ,0) * ISNULL(PRO.ANCHO ,0) * ISNULL(PRO.LARGO ,0) ) * P.CANT_CONFIRMADA),
			P.NRO_SERIE,
			DD.NRO_DESPACHO,
			DD.CAT_LOG_ID,
			DD.NRO_BULTO,
			DD.FECHA_VENCIMIENTO			
	having	SUM(P.CANT_CONFIRMADA)>0  --se agrega para que no salgan en el reporte
	
END

GO


