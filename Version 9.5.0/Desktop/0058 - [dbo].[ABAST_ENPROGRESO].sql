/****** Object:  StoredProcedure [dbo].[ABAST_ENPROGRESO]    Script Date: 05/19/2015 16:19:41 ******/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[ABAST_ENPROGRESO]') AND type in (N'P', N'PC'))
DROP PROCEDURE [dbo].[ABAST_ENPROGRESO]
GO

CREATE  PROCEDURE [dbo].[ABAST_ENPROGRESO]
@P_CLIENTE_ID	VARCHAR(100) OUTPUT,
@P_PRODUCTO_ID	VARCHAR(100) OUTPUT,
@P_POSICION_COD	VARCHAR(100) OUTPUT
AS
BEGIN

	SELECT	'0'										AS [CHK],
			P.POSICION_COD							AS [POSICION_COD],
			DA.PRIORIDAD							AS [PRIORIDAD],
			SU.USUARIO_ID							AS [USUARIO_COD],
			DA.CLIENTE_ID							AS [CLIENTE_COD],
			DA.PRODUCTO_ID							AS [PRODUCTO_COD],
			ISNULL(X.QTY_POS,0)						AS [QTY_POS],
			ISNULL(C.QTY ,	CASE
							WHEN (RL.OCUPACION_MAX - ISNULL(X.QTY_POS,0))>0
							THEN (RL.OCUPACION_MAX - ISNULL(X.QTY_POS,0))
							ELSE 0 END)					AS [QTY_ABAST],
			CONVERT(VARCHAR,DA.F_INICIO,103) 
			+ [dbo].[FxTimebyDetime](DA.F_INICIO)	AS [F_INICIO],
			DA.ABAST_ID,
			CASE DA.EN_PROGRESO WHEN '1' THEN 'SI' ELSE 'NO' END EN_PROGRESO
	FROM	DET_ABASTECIMIENTO DA INNER JOIN POSICION P		ON(DA.POSICION_ID=P.POSICION_ID)
			LEFT JOIN SYS_USUARIO SU						ON(DA.USUARIO=SU.USUARIO_ID)
            LEFT JOIN ( SELECT   DD.CLIENTE_ID
                                ,DD.PRODUCTO_ID
                                ,RL.POSICION_ACTUAL
                                ,SUM(RL.CANTIDAD) AS QTY_POS
                        FROM    RL_DET_DOC_TRANS_POSICION RL
                                INNER JOIN DET_DOCUMENTO_TRANSACCION DDT	ON(RL.DOC_TRANS_ID=DDT.DOC_TRANS_ID AND RL.NRO_LINEA_TRANS=DDT.NRO_LINEA_TRANS)
                                INNER JOIN DET_DOCUMENTO DD                 ON(DDT.DOCUMENTO_ID=DD.DOCUMENTO_ID AND DDT.NRO_LINEA_DOC=DD.NRO_LINEA)
                                INNER JOIN CATEGORIA_LOGICA CL              ON(RL.CLIENTE_ID=CL.CLIENTE_ID AND RL.CAT_LOG_ID=CL.CAT_LOG_ID )
                                INNER JOIN POSICION P                       ON(RL.POSICION_ACTUAL=P.POSICION_ID)
                                LEFT JOIN ESTADO_MERCADERIA_RL EM           ON(RL.CLIENTE_ID=EM.CLIENTE_ID AND RL.EST_MERC_ID=EM.EST_MERC_ID)     
                                INNER JOIN DOCUMENTO D                      ON(DD.DOCUMENTO_ID=D.DOCUMENTO_ID)
                        WHERE    RL.DOC_TRANS_ID_EGR IS NULL
                                AND RL.NRO_LINEA_TRANS_EGR IS NULL
                                AND RL.DISPONIBLE='1'
                                AND ISNULL(EM.DISP_EGRESO,'1')='1'
                                AND ISNULL(EM.PICKING,'1')='1'
                                AND P.POS_LOCKEADA='0' 
                                AND P.PICKING='1'
                                AND CL.DISP_EGRESO='1' 
                                AND CL.PICKING='1'
                                AND RL.CAT_LOG_ID<>'TRAN_EGR'
                        GROUP BY
                                DD.CLIENTE_ID, DD.PRODUCTO_ID, RL.POSICION_ACTUAL)X
																			ON(DA.CLIENTE_ID=X.CLIENTE_ID AND DA.PRODUCTO_ID=X.PRODUCTO_ID AND DA.POSICION_ID=X.POSICION_ACTUAL)
            INNER JOIN RL_PRODUCTO_POSICION_PERMITIDA RL					ON(DA.CLIENTE_ID=RL.CLIENTE_ID AND DA.PRODUCTO_ID=RL.PRODUCTO_ID AND DA.POSICION_ID=RL.POSICION_ID)
            INNER JOIN RL_SYS_CLIENTE_USUARIO RCU							ON(RL.CLIENTE_ID =RCU.CLIENTE_ID)
            LEFT JOIN(	SELECT	ABAST_ID,SUM(CANTIDAD) AS QTY
						FROM	ABAST_CONSUMO_LOCATOR
						GROUP BY
								ABAST_ID 
            )C																ON(DA.ABAST_ID=C.ABAST_ID)
	WHERE	ISNULL(DA.FINALIZADO,'0')='0'
			AND ((@P_CLIENTE_ID IS NULL) OR (DA.CLIENTE_ID=@P_CLIENTE_ID))
			AND ((@P_PRODUCTO_ID IS NULL) OR (DA.PRODUCTO_ID=@P_PRODUCTO_ID))
			AND ((@P_POSICION_COD IS NULL) OR (P.POSICION_COD=@P_POSICION_COD))
			AND RCU.USUARIO_ID IN(SELECT USUARIO_ID FROM #TEMP_USUARIO_LOGGIN)
END
GO


