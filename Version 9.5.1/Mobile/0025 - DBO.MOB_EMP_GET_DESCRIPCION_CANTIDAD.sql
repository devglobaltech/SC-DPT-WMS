/****** Object:  StoredProcedure [dbo].[MOB_EMP_GET_DESCRIPCION_CANTIDAD]    Script Date: 08/17/2015 15:26:27 ******/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[MOB_EMP_GET_DESCRIPCION_CANTIDAD]') AND type in (N'P', N'PC'))
DROP PROCEDURE [dbo].[MOB_EMP_GET_DESCRIPCION_CANTIDAD]
GO

CREATE PROCEDURE [dbo].[MOB_EMP_GET_DESCRIPCION_CANTIDAD]
	@VIAJE_ID		VARCHAR(100),
	@CLIENTE_ID		VARCHAR(15),
	@PRODUCTO_ID	VARCHAR(30),
	@CONTENEDORAS	VARCHAR(4000),
	@PEDIDO			VARCHAR(100)OUTPUT,
	@DESCRIPCION	VARCHAR(200)OUTPUT,
	@CANTIDAD		BIGINT		OUTPUT,
	@SOLICITA_LP	VARCHAR(1)	OUTPUT,
	@LOTE			VARCHAR(100)OUTPUT,
	@PARTIDA		VARCHAR(100)OUTPUT,
	@NRO_SERIE		VARCHAR(100)OUTPUT,
	@CONT_EMPAQUE	VARCHAR(100)OUTPUT,
	@USUARIO		VARCHAR(100)OUTPUT
AS
BEGIN	

	DECLARE @CONT	NUMERIC(20,0)
	DECLARE @EX		NUMERIC(20,0)

	SELECT @EX=COUNT(*)
	FROM	PICKING P
	WHERE	P.CLIENTE_ID=@CLIENTE_ID
			AND P.VIAJE_ID=@VIAJE_ID
			AND P.PRODUCTO_ID=@PRODUCTO_ID

	IF @EX=0 BEGIN					
		RAISERROR('No se encontro el producto %s en la Ola de Picking.',16,1,@PRODUCTO_ID)
		RETURN
	END				
					
	SELECT @CONT=COUNT(*)
	FROM	PICKING P INNER JOIN DBO.SPLIT(@CONTENEDORAS,';') R
			ON(P.PALLET_PICKING=R.STRING)
	WHERE	P.VIAJE_ID=@VIAJE_ID
			AND P.CLIENTE_ID=@CLIENTE_ID
			AND P.PRODUCTO_ID=@PRODUCTO_ID
	GROUP BY
			P.DESCRIPCION	
			
	IF @CONT=0 BEGIN					
		RAISERROR('El producto %s no se encuentra dentro de las contenedoras seleccionadas o no existe en el pedido.',16,1,@PRODUCTO_ID)
		RETURN
	END				
		
	SELECT	TOP 1
			@PEDIDO=D.NRO_REMITO,@DESCRIPCION=P.DESCRIPCION,@CANTIDAD=SUM(P.CANT_CONFIRMADA),@SOLICITA_LP=CP.FLG_EMP_LOTE_PARTIDA,
			@LOTE=P.NRO_LOTE, @PARTIDA=P.NRO_PARTIDA, @CONT_EMPAQUE=M1.UC_EMPAQUE, @NRO_SERIE=P.NRO_SERIE
	FROM	PICKING P INNER JOIN DOCUMENTO D		ON(P.DOCUMENTO_ID=D.DOCUMENTO_ID)			
			INNER JOIN MOB_EMPAQUE_UC_EMPAQUE M1	ON(D.DOCUMENTO_ID=M1.DOCUMENTO_ID AND M1.ESTADO='ABIERTO')
			INNER JOIN CLIENTE_PARAMETROS CP		ON(P.CLIENTE_ID=CP.CLIENTE_ID)
	WHERE	P.VIAJE_ID=@VIAJE_ID
			AND P.CLIENTE_ID=@CLIENTE_ID 
			AND P.PRODUCTO_ID=@PRODUCTO_ID 
			AND P.BULTOS_CONTROLADOS IS NULL
	GROUP BY
			D.NRO_REMITO,P.DESCRIPCION,CP.FLG_EMP_LOTE_PARTIDA, P.NRO_LOTE ,P.NRO_PARTIDA, M1.UC_EMPAQUE, P.NRO_SERIE
			
	UPDATE	PICKING 
	SET		BULTOS_CONTROLADOS=1, USUARIO_PF=@USUARIO
	FROM	PICKING P INNER JOIN DOCUMENTO D		ON(P.DOCUMENTO_ID=D.DOCUMENTO_ID)			
			INNER JOIN MOB_EMPAQUE_UC_EMPAQUE M1	ON(D.DOCUMENTO_ID=M1.DOCUMENTO_ID AND M1.ESTADO='ABIERTO')
			INNER JOIN CLIENTE_PARAMETROS CP		ON(P.CLIENTE_ID=CP.CLIENTE_ID)
	WHERE	P.VIAJE_ID=@VIAJE_ID
			AND P.CLIENTE_ID=@CLIENTE_ID 
			AND P.PRODUCTO_ID=@PRODUCTO_ID 
			AND D.NRO_REMITO =@PEDIDO	
			AND ((@LOTE IS NULL AND P.NRO_LOTE IS NULL)OR(P.NRO_LOTE=@LOTE))
			AND ((@PARTIDA IS NULL AND P.NRO_PARTIDA IS NULL)OR(P.NRO_PARTIDA=@PARTIDA))
			AND ((@NRO_SERIE IS NULL AND P.NRO_SERIE IS NULL)OR(P.NRO_SERIE=@NRO_SERIE))
					
	IF @CANTIDAD IS NULL BEGIN					
		RAISERROR('El producto %s ya ha sido empaquetado.',16,1,@PRODUCTO_ID)
	END
	
END

