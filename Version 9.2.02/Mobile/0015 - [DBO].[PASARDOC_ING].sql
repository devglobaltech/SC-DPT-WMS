IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[DBO].[PASARDOC_ING]') AND type in (N'P', N'PC'))
DROP PROCEDURE [DBO].[PASARDOC_ING]
GO
/*
 ================================================================================================
 Author:		S.GOMEZ.
 Create date:	06/08/2014.
 
 Descripción:
 Procedimiento generado para el cierre automatico de los documentos de ingreso.
 ================================================================================================
*/
CREATE PROCEDURE [DBO].[PASARDOC_ING]
	@DOC_TRANS_ID	NUMERIC(20,0)
AS

BEGIN
	SET XACT_ABORT ON;
	DECLARE @PCUR_RL	CURSOR;
	DECLARE	@RL_ID		NUMERIC(20,0);
	DECLARE @USUARIO	VARCHAR(100);
	
	SET @USUARIO='ADMIN'
	
	--PASAJE A HISTORICOS DE TODO EL MATERIAL.
	EXEC DBO.FIN_DOCUMENTO_HIST_INSERT @DOC_TRANS_ID, 2;

	IF CURSOR_STATUS('global','@PCUR_RL')>=-1 BEGIN
		DEALLOCATE @PCUR_RL
	END
	
	SET @PCUR_RL = CURSOR FOR
	SELECT	RL_ID
	FROM	RL_DET_DOC_TRANS_POSICION 
	WHERE	DOC_TRANS_ID = @DOC_TRANS_ID 	
	
	OPEN @PCUR_RL
	FETCH NEXT FROM @PCUR_RL INTO @RL_ID;
	WHILE @@FETCH_STATUS=0 BEGIN
		EXEC ACTUALIZAR_HISTORICOS_X_MOV @RL_ID, @USUARIO; 
		FETCH NEXT FROM @PCUR_RL INTO @RL_ID;
	END
	
	UPDATE RL_DET_DOC_TRANS_POSICION SET CAT_LOG_ID=CAT_LOG_ID_FINAL, DISPONIBLE = '1' WHERE DOC_TRANS_ID = @DOC_TRANS_ID;
	
	--PASO LA RL A HISTORICOS
	INSERT INTO RL_DET_DOC_TR_POS_HIST    
	SELECT	DOC_TRANS_ID   
			,NRO_LINEA_TRANS   
			,POSICION_ANTERIOR   
			,POSICION_ACTUAL   
			,CANTIDAD   
			,TIPO_MOVIMIENTO_ID   
			,ULTIMA_ESTACION   
			,ULTIMA_SECUENCIA   
			,NAVE_ANTERIOR   
			,NAVE_ACTUAL   
			,DOCUMENTO_ID   
			,NRO_LINEA   
			,DISPONIBLE   
			,DOC_TRANS_ID_EGR   
			,NRO_LINEA_TRANS_EGR   
			,DOC_TRANS_ID_TR   
			,NRO_LINEA_TRANS_TR   
			,CLIENTE_ID   
			,CAT_LOG_ID  
			,CAT_LOG_ID_FINAL   
			,EST_MERC_ID   
	FROM	RL_DET_DOC_TRANS_POSICION   
	WHERE	DOC_TRANS_ID = @DOC_TRANS_ID;

	UPDATE	DOCUMENTO_TRANSACCION 
	SET		ESTACION_ACTUAL = NULL ,
			STATUS = 'T40' ,
			FECHA_FIN_GTW = GETDATE(),
			TR_ACTIVO = 0,
			TR_ACTIVO_ID = NULL,
			SESSION_ID = NULL,
			FECHA_CAMBIO_TR = NULL			
	WHERE	DOC_TRANS_ID = @DOC_TRANS_ID 	
	
	UPDATE	DOCUMENTO SET STATUS = 'D40', FECHA_FIN_GTW = GETDATE() 
	FROM	DOCUMENTO D INNER JOIN DET_DOCUMENTO DD
			ON(D.DOCUMENTO_ID=DD.DOCUMENTO_ID)
			INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
			ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
	WHERE	DDT.DOC_TRANS_ID=@DOC_TRANS_ID

END

