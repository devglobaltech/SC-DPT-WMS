
/****** Object:  StoredProcedure [dbo].[PERMITE_PICK_DE_SERIE]    Script Date: 08/28/2014 15:18:26 ******/
IF EXISTS(SELECT *
          FROM sys.objects
          WHERE object_id=OBJECT_ID(N'[dbo].[PERMITE_PICK_DE_SERIE]') AND type IN(N'P',N'PC'))
DROP PROCEDURE [dbo].[PERMITE_PICK_DE_SERIE]
GO
CREATE PROCEDURE [dbo].[PERMITE_PICK_DE_SERIE]
(
 @CLIENTE_ID VARCHAR(15),@PRODUCTO_ID VARCHAR(30),@NRO_SERIE_ANT VARCHAR(50),@NRO_SERIE VARCHAR(50),@CODIGO_VIAJE VARCHAR(100),@USUARIO_ID VARCHAR(30),@OUT VARCHAR(1) OUTPUT
)
AS 
BEGIN
    DECLARE @STATUSDOC AS VARCHAR(20)
    DECLARE @NRO_LOTE_ANT AS VARCHAR(100)
    DECLARE @NRO_PARTIDA_ANT AS VARCHAR(100)
    DECLARE @NRO_LOTE_NEW AS VARCHAR(100)
    DECLARE @NRO_PARTIDA_NEW AS VARCHAR(100)
    
    SET @OUT='5'
    
--ESTA ES SI O SI. ES LA SERIE QUE INGRESO, VALIDO QUE EXISTA AL MENOS INGRESADA
    
    SELECT	@STATUSDOC=D.STATUS
    FROM	RL_DET_DOC_TRANS_POSICION RL
			INNER JOIN DET_DOCUMENTO_TRANSACCION DDT ON (RL.DOC_TRANS_ID=DDT.DOC_TRANS_ID AND RL.NRO_LINEA_TRANS=DDT.NRO_LINEA_TRANS)
			INNER JOIN DET_DOCUMENTO DD ON (DDT.DOCUMENTO_ID=DD.DOCUMENTO_ID AND DDT.NRO_LINEA_DOC=DD.NRO_LINEA)
			INNER JOIN DOCUMENTO D ON (DD.DOCUMENTO_ID=D.DOCUMENTO_ID)
    WHERE	DD.NRO_SERIE=@NRO_SERIE AND DD.PRODUCTO_ID=@PRODUCTO_ID AND D.CLIENTE_ID=@CLIENTE_ID
    
    IF ISNULL(@STATUSDOC,'')=''
    BEGIN
        SET @OUT='1' --LA SERIE NO EXISTE
        
        RETURN
    END
    
    IF @STATUSDOC<>'D40'
    BEGIN
        SET @OUT='2' -- LA SERIE NO FUE UBICADA. (NO SE ENCUENTRA DISPONIBLE PARA EGRESO.)
        
        RETURN
    END
    
	--VALIDO SI LA SERIE CORRESPONDE AL VIAJE Y QUE NO ESTE PICKEADA
    IF EXISTS(SELECT 1
              FROM PICKING
              WHERE CLIENTE_ID=@CLIENTE_ID AND VIAJE_ID=@CODIGO_VIAJE AND NRO_SERIE=@NRO_SERIE AND FECHA_FIN IS NULL AND PRODUCTO_ID=@PRODUCTO_ID AND (USUARIO IS NULL OR USUARIO=@USUARIO_ID))
    BEGIN
        SET @OUT='0' --OK, LA SERIE ES VALIDA
        
        RETURN
    END
    
    IF EXISTS(SELECT 1
              FROM PICKING
              WHERE CLIENTE_ID=@CLIENTE_ID AND VIAJE_ID=@CODIGO_VIAJE AND NRO_SERIE=@NRO_SERIE AND FECHA_FIN IS NOT NULL AND PRODUCTO_ID=@PRODUCTO_ID)
    BEGIN
        SET @OUT='3' --LA SERIE YA FUE PICKEADA
        
        RETURN
    END --VALIDO SI

    --SI PERTENECE A OTRO VIAJE ME FIJO QUE NO ESTE EXPLICITADA EN EL DOCUMENTO DE EGRESO DE ESE VIAJE
    IF EXISTS(SELECT	1
              FROM		PICKING P
						INNER JOIN DET_DOCUMENTO DDEGR ON (P.DOCUMENTO_ID=DDEGR.DOCUMENTO_ID AND P.NRO_LINEA=DDEGR.NRO_LINEA AND P.NRO_SERIE=DDEGR.NRO_SERIE)
              WHERE		P.VIAJE_ID<>@CODIGO_VIAJE AND P.NRO_SERIE=@NRO_SERIE AND P.FECHA_FIN IS NULL AND P.PRODUCTO_ID=@PRODUCTO_ID AND P.CLIENTE_ID=@CLIENTE_ID)
    BEGIN
        SET @OUT='4' --LA SERIE PERTENECE A OTRO EGRESO Y NO PUEDE CAMBIARSE
        
        RETURN
    END
    
	--VALIDO QUE LA SERIE NO ESTE EXPLICITADA EN NINGUN DOCUMENTO DE EGRESO PENDIENTE.
    IF EXISTS(SELECT 1
              FROM PICKING P
              INNER JOIN DET_DOCUMENTO DE ON (P.DOCUMENTO_ID=DE.DOCUMENTO_ID AND P.NRO_LINEA=DE.NRO_LINEA)
              WHERE FECHA_FIN IS NULL AND DE.NRO_SERIE=@NRO_SERIE AND DE.PRODUCTO_ID=@PRODUCTO_ID
                    )
    BEGIN
        SET @OUT='5'
    END
    
	--SI NO VALIDA NINGUNA DE LAS ANTERIORES BUSCO QUE LA SERIE ESTE DISPONIBLE PARA EGRESO
    IF EXISTS(SELECT 1
              FROM (SELECT	RL.*
                    FROM	RL_DET_DOC_TRANS_POSICION RL
							INNER JOIN DET_DOCUMENTO_TRANSACCION DDT ON (RL.DOC_TRANS_ID=DDT.DOC_TRANS_ID AND RL.NRO_LINEA_TRANS=DDT.NRO_LINEA_TRANS)
							INNER JOIN DET_DOCUMENTO DD ON (DDT.DOCUMENTO_ID=DD.DOCUMENTO_ID AND DDT.NRO_LINEA_DOC=DD.NRO_LINEA)
							INNER JOIN DOCUMENTO D ON (DD.DOCUMENTO_ID=D.DOCUMENTO_ID)
							INNER JOIN CATEGORIA_LOGICA CL ON (RL.CLIENTE_ID=CL.CLIENTE_ID AND RL.CAT_LOG_ID=CL.CAT_LOG_ID)
							LEFT JOIN ESTADO_MERCADERIA_RL E ON (RL.CLIENTE_ID=E.CLIENTE_ID AND RL.EST_MERC_ID=E.EST_MERC_ID)
							INNER JOIN NAVE N ON (RL.NAVE_ACTUAL=N.NAVE_ID)
                    WHERE D.STATUS='D40' AND RL.DISPONIBLE='1' AND N.DISP_EGRESO='1' AND N.PICKING='1' AND CL.DISP_EGRESO='1' AND CL.PICKING='1' AND ((E.DISP_EGRESO='1' AND E.PICKING='1') OR (RL.EST_MERC_ID IS NULL)) AND DD.NRO_SERIE=@NRO_SERIE AND RL.DOC_TRANS_ID_EGR IS NULL AND RL.NRO_LINEA_TRANS_EGR IS NULL AND DD.PRODUCTO_ID=@PRODUCTO_ID AND D.CLIENTE_ID=@CLIENTE_ID
                    UNION
                    SELECT	RL.*
                    FROM	RL_DET_DOC_TRANS_POSICION RL
							INNER JOIN DET_DOCUMENTO_TRANSACCION DDT ON (RL.DOC_TRANS_ID=DDT.DOC_TRANS_ID AND RL.NRO_LINEA_TRANS=DDT.NRO_LINEA_TRANS)
							INNER JOIN DET_DOCUMENTO DD ON (DDT.DOCUMENTO_ID=DD.DOCUMENTO_ID AND DDT.NRO_LINEA_DOC=DD.NRO_LINEA)
							INNER JOIN DOCUMENTO D ON (DD.DOCUMENTO_ID=D.DOCUMENTO_ID)
							INNER JOIN CATEGORIA_LOGICA CL ON (RL.CLIENTE_ID=CL.CLIENTE_ID AND RL.CAT_LOG_ID=CL.CAT_LOG_ID)
							LEFT JOIN ESTADO_MERCADERIA_RL E ON (RL.CLIENTE_ID=E.CLIENTE_ID AND RL.EST_MERC_ID=E.EST_MERC_ID)
							INNER JOIN POSICION P ON (RL.POSICION_ACTUAL=P.POSICION_ID)
                    WHERE	D.STATUS='D40' AND RL.DISPONIBLE='1' AND P.PICKING='1' AND CL.DISP_EGRESO='1' AND CL.PICKING='1' AND ((E.DISP_EGRESO='1' AND E.PICKING='1') OR (RL.EST_MERC_ID IS NULL)) AND DD.NRO_SERIE=@NRO_SERIE AND RL.DOC_TRANS_ID_EGR IS NULL AND RL.NRO_LINEA_TRANS_EGR IS NULL AND DD.PRODUCTO_ID=@PRODUCTO_ID AND D.CLIENTE_ID=@CLIENTE_ID) X)
	BEGIN                    
		SET @OUT='0' --TODO OK, ESTA DISPONIBLE PARA EGRESO
    END
    ELSE 
    BEGIN
		IF EXISTS(	SELECT	1
					FROM	PICKING P INNER JOIN DET_DOCUMENTO DE 
							ON (P.DOCUMENTO_ID=DE.DOCUMENTO_ID AND P.NRO_LINEA=DE.NRO_LINEA)
					WHERE	FECHA_FIN IS NULL 
							AND DE.CLIENTE_ID=@CLIENTE_ID
							AND DE.PRODUCTO_ID=@PRODUCTO_ID
							AND P.NRO_SERIE=@NRO_SERIE)
		BEGIN
			SET @OUT='0'
		END
		ELSE
		BEGIN							
			--NO ESTA DISPONIBLE PARA EGRESO
			SET @OUT='5'
		END
    END
	
    
END
GO

