/****** Object:  UserDefinedFunction [dbo].[GUARDADO_CONTENEDOR_INGRESO_VOLUMEN]    Script Date: 01/16/2015 15:18:20 ******/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[GUARDADO_SC_INGRESO_VOLUMEN_PESO]') AND type in (N'FN', N'IF', N'TF', N'FS', N'FT'))
DROP FUNCTION [dbo].[GUARDADO_SC_INGRESO_VOLUMEN_PESO]
GO

CREATE FUNCTION DBO.GUARDADO_SC_INGRESO_VOLUMEN_PESO(
	@DOCUMENTO_ID	NUMERIC(20,0),
	@NRO_LINEA		NUMERIC(10,0),
	@POSICION_ID	NUMERIC(20,0)
)RETURNS NUMERIC
AS
BEGIN
	DECLARE @POS_VOLUMEN	NUMERIC(20,9)
	DECLARE @POS_VOL_OCU	NUMERIC(20,9)
	DECLARE @PROD_VOLUMEN	NUMERIC(20,9)
	DECLARE @PCJ_OCUPACION	NUMERIC(20,2)
	DECLARE @OCUPACION		NUMERIC(20,9)
	DECLARE @RETORNO		NUMERIC(20,5)
	DECLARE @STK_PESO		NUMERIC(20,5)
	DECLARE @POS_PESO		NUMERIC(20,5)
	DECLARE @PESO_UN		NUMERIC(20,5)
	DECLARE @PESO_RT		NUMERIC(20,5)
	DECLARE @PESO_RET		NUMERIC(20,5)
	DECLARE @CANT_UNI		NUMERIC(20,5)
	DECLARE @CANTIDAD		NUMERIC(20,5)
	DECLARE @CONT			NUMERIC(10,0)
	
	SELECT	@CANTIDAD=SUM(RL.CANTIDAD)
	FROM	DOCUMENTO D INNER JOIN DET_DOCUMENTO DD		ON(D.DOCUMENTO_ID=DD.DOCUMENTO_ID)
			INNER JOIN DET_DOCUMENTO_TRANSACCION DDT	ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
			INNER JOIN RL_DET_DOC_TRANS_POSICION RL		ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
			INNER JOIN NAVE N							ON(RL.NAVE_ACTUAL=N.NAVE_ID AND N.PRE_INGRESO='1')
	WHERE	DD.DOCUMENTO_ID=@DOCUMENTO_ID
			AND DD.NRO_LINEA=@NRO_LINEA
	
	IF @CANTIDAD IS NULL
	BEGIN 
		RETURN 0
	END 
	----------------------------------------------------------------------------------------------------------------------------------	
	--1. OBTENGO LA VOLUMETRIA DE LA POSICION Y EL PCJ DE OCUPACION. SINO ESTA PRESENTE SALE Y SE PUEDE UBICAR.
	----------------------------------------------------------------------------------------------------------------------------------	
	SELECT	@POS_VOLUMEN	=(ISNULL(ALTO,0) * ISNULL(ANCHO,0) * ISNULL(LARGO,0))/1000000,
			@PCJ_OCUPACION	=PCJ_OCUPACION,
			@POS_PESO		=PESO,
			@CONT			=COUNT(POSICION_ID)
	FROM	POSICION
	WHERE	POSICION_ID=@POSICION_ID
	GROUP BY
			(ISNULL(ALTO,0) * ISNULL(ANCHO,0) * ISNULL(LARGO,0))/1000000,
			PCJ_OCUPACION,
			PESO
	
	IF @POS_VOLUMEN=0
	BEGIN
		--NO SE CARGARON LOS DATOS DE VOLUMETRIA DE LA POSICION. ASI QUE NO TIENE SENTIDO CONTINUAR CON EL ANALISIS.
		RETURN @CANTIDAD
	END	
	
	IF @CONT=0
	BEGIN
		RETURN @CANTIDAD	
	END		
	----------------------------------------------------------------------------------------------------------------------------------	
	--2. OBTENGO LA VOLUMETRIA DEL PRODUCTO. SINO ESTA PRESENTE SALE Y SE PUEDE UBICAR.
	----------------------------------------------------------------------------------------------------------------------------------
	SELECT	@PROD_VOLUMEN=(SUM(X.VOL_UN)/1000000),
			@PESO_UN=(SUM(X.PESO_UN))
	FROM	(	SELECT	PR.PRODUCTO_ID,ISNULL(PR.ALTO,0) * ISNULL(PR.ANCHO,0) * ISNULL(PR.LARGO,0) AS VOL_UN, ISNULL(PR.PESO,0) AS PESO_UN
				FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
						ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
						INNER JOIN RL_DET_DOC_TRANS_POSICION RL 
						ON(RL.DOC_TRANS_ID=DDT.DOC_TRANS_ID AND RL.NRO_LINEA_TRANS=DDT.NRO_LINEA_TRANS)
						INNER JOIN PRODUCTO PR
						ON(DD.CLIENTE_ID=PR.CLIENTE_ID AND DD.PRODUCTO_ID=PR.PRODUCTO_ID)
				WHERE	DD.DOCUMENTO_ID=@DOCUMENTO_ID
						AND DD.NRO_LINEA=@NRO_LINEA
				GROUP BY
						PR.PRODUCTO_ID,ISNULL(PR.ALTO,0), ISNULL(PR.ANCHO,0), ISNULL(PR.LARGO,0),ISNULL(PR.PESO,0)
			)X
			
	IF @PROD_VOLUMEN=0
	BEGIN
		RETURN @CANTIDAD
	END		
		
	----------------------------------------------------------------------------------------------------------------------------------
	--3. OBTENGO LA OCUPACION ACTUAL DE LA POSICION.
	----------------------------------------------------------------------------------------------------------------------------------	
	SELECT	@POS_VOL_OCU=SUM(X.QTY)* (SUM(X.VOL_UN)/1000000)
	FROM	(	SELECT	DD.PRODUCTO_ID,SUM(RL.CANTIDAD) AS QTY, ISNULL(PR.ALTO,0) * ISNULL(PR.ANCHO,0) * ISNULL(PR.LARGO,0) AS VOL_UN
				FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
						ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
						INNER JOIN RL_DET_DOC_TRANS_POSICION RL 
						ON(RL.DOC_TRANS_ID=DDT.DOC_TRANS_ID AND RL.NRO_LINEA_TRANS=DDT.NRO_LINEA_TRANS)
						INNER JOIN PRODUCTO PR
						ON(DD.CLIENTE_ID=PR.CLIENTE_ID AND DD.PRODUCTO_ID=PR.PRODUCTO_ID)
				WHERE	RL.POSICION_ACTUAL=@POSICION_ID	
				GROUP BY
						DD.PRODUCTO_ID,  ISNULL(PR.ALTO,0), ISNULL(PR.ANCHO,0), ISNULL(PR.LARGO,0)
			)X
			
	----------------------------------------------------------------------------------------------------------------------------------	
	--4. ANALIZO SI SE OCUPA TODO O UN PORCENTAJE DE LA UBICACION.
	----------------------------------------------------------------------------------------------------------------------------------	
	IF @PCJ_OCUPACION<>100
	BEGIN
		SET @POS_VOLUMEN=((@POS_VOLUMEN*@PCJ_OCUPACION)/100)
	END
	
	----------------------------------------------------------------------------------------------------------------------------------	
	--5. ANALIZO EFECTIVAMENTE SI ENTRA O NO EN LA POSICION EL PALLET.
	----------------------------------------------------------------------------------------------------------------------------------	
	SET @POS_VOLUMEN=ISNULL(@POS_VOLUMEN,0) - ISNULL(@POS_VOL_OCU,0)--ACA DESCUENTO LA OCUPACION DE LA POSICION.
	
	SET @CANT_UNI=FLOOR(@POS_VOLUMEN/ISNULL(@PROD_VOLUMEN,1))
	
	IF (@CANT_UNI>@CANTIDAD)
	BEGIN
		SET @CANT_UNI=@CANTIDAD
	END
	--6. TENGO QUE PENSAR EN EL PESO.
	
	SELECT	@STK_PESO=SUM(X.RESULTADO)
	FROM	(	SELECT	RL.CANTIDAD * ISNULL(P.PESO,0) AS RESULTADO
				FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
						ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_TRANS)
						INNER JOIN RL_DET_DOC_TRANS_POSICION RL
						ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
						INNER JOIN PRODUCTO P
						ON(DD.CLIENTE_ID=P.CLIENTE_ID AND DD.PRODUCTO_ID=P.PRODUCTO_ID)
				WHERE	RL.POSICION_ACTUAL=@POSICION_ID
			)X	
	
	SET @POS_PESO=@POS_PESO-ISNULL(@STK_PESO,0)
	
	IF ISNULL(@POS_PESO,0)=0 
	BEGIN
		SET @POS_PESO=@CANT_UNI
	END
	
	SET @PESO_RET=FLOOR(@POS_PESO/ISNULL(@PESO_UN,1))
	IF @PESO_RET>@CANTIDAD
	BEGIN
		SET @PESO_RET=@CANTIDAD
	END 
	
	IF @CANT_UNI>@PESO_RET
	BEGIN
		SET @RETORNO=@PESO_RET
	END
	ELSE
	BEGIN
		SET @RETORNO=@CANT_UNI
	END
	
	IF @RETORNO<0 
	BEGIN
		SET @RETORNO=0
	END

	RETURN @RETORNO	
END

