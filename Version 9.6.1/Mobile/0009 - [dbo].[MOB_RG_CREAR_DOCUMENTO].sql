/****** Object:  StoredProcedure [dbo].[MOB_RG_CREAR_DOCUMENTO]    Script Date: 12/15/2015 18:35:35 ******/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[MOB_RG_CREAR_DOCUMENTO]') AND type in (N'P', N'PC'))
DROP PROCEDURE [dbo].[MOB_RG_CREAR_DOCUMENTO]
GO

CREATE PROCEDURE [dbo].[MOB_RG_CREAR_DOCUMENTO]
@PNRO_PALLET	AS VARCHAR(100),
@DOCUMENTO_ID	AS NUMERIC(20,0) OUTPUT
AS
BEGIN
	
	SET XACT_ABORT ON
	
	DECLARE @CLIENTE_ID			AS VARCHAR(15),
			@TIPO_COMPROBANTE	AS VARCHAR(30),
			@SUCURSAL_ORIGEN	AS VARCHAR(30),
			@DOC_ID				AS NUMERIC(20,0),
			@OC					AS VARCHAR(100),
			@CBACK				AS CURSOR,
			@DOC_EXT			AS VARCHAR(100),
			@NRO_LINEA			AS NUMERIC(10,0),
			@GENERAR			AS VARCHAR(1),
			@DIFERENCIA			AS NUMERIC(20,5),
			@MSG				AS VARCHAR(100),
			@ITERACION			AS NUMERIC(20,0),
			@CONTROL			AS NUMERIC(20,0),
			@RCP_ID				AS NUMERIC(20,0),
			@CPALLET			AS CURSOR
			
	IF CURSOR_STATUS('global','@CBACK')>=-1
	BEGIN
		DEALLOCATE @CBACK
	END			

	IF CURSOR_STATUS('global','@CPALLET')>=-1
	BEGIN
		DEALLOCATE @CPALLET
	END			
		
	SELECT	@CONTROL=COUNT(*)
	FROM	RECEPCION_GUARDADO
	WHERE	NRO_PALLET=@PNRO_PALLET
	
	IF @CONTROL=0 BEGIN
		RAISERROR('No se registro ningun ingreso de material al pallet.',16,1)
		RETURN
	END
			
	SELECT	@CLIENTE_ID=R.CLIENTE_ID,
			@TIPO_COMPROBANTE=CASE  ISNULL(R.ORDEN_DE_COMPRA,'NULL') WHEN 'NULL' THEN 'IM' ELSE 'DO' END,
			@OC=R.ORDEN_DE_COMPRA,
			@SUCURSAL_ORIGEN=SD.AGENTE_ID
	FROM	RECEPCION_GUARDADO R LEFT JOIN SYS_INT_DOCUMENTO SD
			ON(R.CLIENTE_ID=SD.CLIENTE_ID AND R.ORDEN_DE_COMPRA =SD.ORDEN_DE_COMPRA)
	WHERE	R.NRO_PALLET = @PNRO_PALLET
	
	
	--CREACION DE LA CABECERA.
	INSERT INTO DOCUMENTO (CLIENTE_ID,TIPO_COMPROBANTE_ID,TIPO_OPERACION_ID,DET_TIPO_OPERACION_ID,SUCURSAL_ORIGEN,ORDEN_DE_COMPRA,STATUS,FECHA_ALTA_GTW,FECHA_CPTE)
	VALUES(	@CLIENTE_ID,@TIPO_COMPROBANTE,'ING','MAN',@SUCURSAL_ORIGEN,@OC,'D05',GETDATE(),GETDATE())

	--RECUPERO EL ID DEL NUEVO DOCUMENTO
	SET @DOC_ID=SCOPE_IDENTITY()
	
	
	SET @CPALLET = CURSOR FOR
		SELECT	RCP_ID
		FROM	RECEPCION_GUARDADO
		WHERE	NRO_PALLET=@PNRO_PALLET
	
	OPEN @CPALLET 
	
	SET @ITERACION =0
	
	FETCH NEXT FROM @CPALLET INTO @RCP_ID
	WHILE @@FETCH_STATUS=0 BEGIN
	
		SET @ITERACION = @ITERACION + 1
		
		INSERT INTO DET_DOCUMENTO (	DOCUMENTO_ID,NRO_LINEA,CLIENTE_ID,PRODUCTO_ID,CANTIDAD,CAT_LOG_ID,DESCRIPCION,
									NRO_LOTE, FECHA_VENCIMIENTO,NRO_PARTIDA, UNIDAD_ID,BUSC_INDIVIDUAL,TIE_IN,ITEM_OK,
									CAT_LOG_ID_FINAL,PROP1,VOLUMEN_UNITARIO,PESO_UNITARIO,PROP2, NRO_BULTO) 
		SELECT	TOP 1
				@DOC_ID,
				@ITERACION,
				RG.CLIENTE_ID,
				RG.PRODUCTO_ID,
				RG.CANTIDAD,
				'TRAN_ING',
				PR.DESCRIPCION,
				RG.NRO_LOTE,
				RG.F_VENCIMIENTO,
				RG.NRO_PARTIDA,
				PR.UNIDAD_ID,
				'1',
				'0',
				'1',
				ISNULL(RCAT.CAT_LOG_ID,PR.ING_CAT_LOG_ID),
				RG.NRO_PALLET,
				'1',
				'1',
				SDD.DOC_EXT,
				RG.NRO_BULTO 
		FROM	RECEPCION_GUARDADO RG INNER JOIN PRODUCTO PR
				ON(RG.CLIENTE_ID=PR.CLIENTE_ID AND RG.PRODUCTO_ID=PR.PRODUCTO_ID)
				LEFT JOIN(	SELECT	CLIENTE_ID,
									PRODUCTO_ID,
									CAT_LOG_ID,
									TIPO_COMPROBANTE_ID
							FROM	RL_PRODUCTO_CATLOG
							WHERE	TIPO_OPERACION_ID='ING'
				)RCAT
				ON(RCAT.CLIENTE_ID=RG.CLIENTE_ID AND RCAT.PRODUCTO_ID=PR.PRODUCTO_ID AND RCAT.TIPO_COMPROBANTE_ID=CASE  ISNULL(RG.ORDEN_DE_COMPRA,'NULL') WHEN 'NULL' THEN 'IM' ELSE 'DO' END)
				LEFT JOIN SYS_INT_DOCUMENTO SD
				ON(RG.CLIENTE_ID=SD.CLIENTE_ID AND RG.ORDEN_DE_COMPRA=SD.ORDEN_DE_COMPRA)
				LEFT JOIN SYS_INT_DET_DOCUMENTO SDD
				ON(SD.CLIENTE_ID=SDD.CLIENTE_ID AND SD.DOC_EXT=SDD.DOC_EXT)
		WHERE	RG.NRO_PALLET=@PNRO_PALLET
				AND RG.RCP_ID =@RCP_ID 
				AND ((SDD.PRODUCTO_ID IS NULL)OR(RG.PRODUCTO_ID=SDD.PRODUCTO_ID))
				AND SDD.ESTADO_GT IS NULL
				
		FETCH NEXT FROM @CPALLET INTO @RCP_ID				
	END
	CLOSE @CPALLET
	DEALLOCATE @CPALLET
				
	UPDATE DOCUMENTO SET STATUS='D10' WHERE DOCUMENTO_ID=@DOC_ID
	------------------------------------------------------------------------
	--APRUEBO DOCUMENTO.
	------------------------------------------------------------------------
	EXEC dbo.Funciones_Stock_Api#Documento_A_Ingreso @DOC_ID
	
	------------------------------------------------------------------------
	--ASIGNO TRATAMIENTO.
	------------------------------------------------------------------------
	EXEC dbo.Asigna_Tratamiento#Asigna_Tratamiento_ING @DOC_ID
	
	------------------------------------------------------------------------
	--ENVIO A AUDITORIAS.
	------------------------------------------------------------------------
	EXEC dbo.AUDITORIA_HIST_INSERT_ING @DOC_ID
		
	------------------------------------------------------------------------
	--RECUPERO EL DOCUMENTO.
	------------------------------------------------------------------------
	SET @DOCUMENTO_ID=@DOC_ID
	
	------------------------------------------------------------------------
	--GENERO LOS BACK_ORDERS SI CORRESPONDE.
	------------------------------------------------------------------------
	SET @CBACK =CURSOR FOR
		SELECT	DISTINCT 
				RG.CLIENTE_ID,
				SDD.DOC_EXT,
				MAX(SDD.NRO_LINEA),
				ISNULL(PR.BACK_ORDER,0)BACK_ORDER,
				SDD.CANTIDAD_SOLICITADA - SUM(RG.CANTIDAD)AS DIF
		FROM	RECEPCION_GUARDADO RG INNER JOIN SYS_INT_DOCUMENTO SD
				ON(RG.CLIENTE_ID=SD.CLIENTE_ID AND RG.ORDEN_DE_COMPRA=SD.ORDEN_DE_COMPRA)
				INNER JOIN SYS_INT_DET_DOCUMENTO SDD
				ON(SD.CLIENTE_ID=SDD.CLIENTE_ID AND SD.DOC_EXT=SDD.DOC_EXT)
				INNER JOIN PRODUCTO PR
				ON(RG.CLIENTE_ID=PR.CLIENTE_ID AND RG.PRODUCTO_ID=PR.PRODUCTO_ID)
		WHERE	RG.NRO_PALLET=@PNRO_PALLET
				AND RG.PRODUCTO_ID=SDD.PRODUCTO_ID
				AND SDD.ESTADO_GT IS NULL
				AND RG.ORDEN_DE_COMPRA IS NOT NULL	
				AND RG.PROCESADO='0'
				AND SDD.DOC_EXT IN(SELECT DISTINCT PROP2 FROM DET_DOCUMENTO WHERE DOCUMENTO_ID=@DOC_ID)
		GROUP BY
				RG.CLIENTE_ID, SDD.DOC_EXT,ISNULL(PR.BACK_ORDER,0),SDD.CANTIDAD_SOLICITADA

				
	SET @ITERACION=0
	OPEN @CBACK
	FETCH NEXT FROM @CBACK INTO @CLIENTE_ID, @DOC_EXT, @NRO_LINEA, @GENERAR, @DIFERENCIA
	WHILE @@FETCH_STATUS=0 BEGIN
		
		SET @ITERACION=@ITERACION + 1
		
		IF @GENERAR='1' BEGIN
			IF @DIFERENCIA>0 BEGIN
				--GENERAR EL BACK ORDER.
				INSERT INTO SYS_INT_DET_DOCUMENTO
				SELECT	TOP 1
						DOC_EXT, @NRO_LINEA + 1  ,CLIENTE_ID, PRODUCTO_ID, @DIFERENCIA ,CANTIDAD , EST_MERC_ID, CAT_LOG_ID, NRO_BULTO, DESCRIPCION, NRO_LOTE, NRO_PALLET, FECHA_VENCIMIENTO, NRO_DESPACHO,
						NRO_PARTIDA,UNIDAD_ID, UNIDAD_CONTENEDORA_ID, PESO, UNIDAD_PESO, VOLUMEN, UNIDAD_VOLUMEN, PROP1, PROP2, PROP3, LARGO, ALTO, ANCHO, NULL, NULL, NULL,NULL,NULL,NULL,
						NULL,NULL,CUSTOMS_1,CUSTOMS_2,CUSTOMS_3
				FROM	SYS_INT_DET_DOCUMENTO
				WHERE	DOC_EXT=@DOC_EXT
						AND NRO_LINEA=@NRO_LINEA
						
				UPDATE SYS_INT_DET_DOCUMENTO SET DOC_BACK_ORDER=DOC_EXT WHERE DOC_EXT=@DOC_EXT AND NRO_LINEA=@NRO_LINEA
				
			END
		END
			
		UPDATE	SYS_INT_DET_DOCUMENTO SET ESTADO_GT='P', FECHA_ESTADO_GT=GETDATE(),DOCUMENTO_ID=@DOCUMENTO_ID
		WHERE	DOC_EXT=@DOC_EXT
				AND NRO_LINEA=@NRO_LINEA
						
		FETCH NEXT FROM @CBACK INTO @CLIENTE_ID, @DOC_EXT, @NRO_LINEA, @GENERAR, @DIFERENCIA
	END--FIN @CBACK
	SET @MSG ='CANTIDAD DE ITERACIONES ' + CAST(@ITERACION AS VARCHAR)
	
	CLOSE @CBACK 
	DEALLOCATE @CBACK			
	
	------------------------------------------------------------------------
	--MARCO PROCESAMIENTO.
	------------------------------------------------------------------------
	UPDATE	RECEPCION_GUARDADO 
	SET		PROCESADO='1'
	WHERE	NRO_PALLET=@PNRO_PALLET 
		
END
GO


