IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[DBO].[MOB_CAMBIO_PALLET_VALIDACION]') AND type in (N'P', N'PC'))
DROP PROCEDURE [DBO].[MOB_CAMBIO_PALLET_VALIDACION]
GO

CREATE PROCEDURE [DBO].[MOB_CAMBIO_PALLET_VALIDACION]
	@CLIENTE_ID		VARCHAR(15),
	@PALLET_ORIGEN	VARCHAR(100),
	@PALLET_DESTINO	VARCHAR(100),
	@CAMBIO_OK		VARCHAR(1)	OUTPUT
AS
BEGIN
	DECLARE @CONT		NUMERIC(20,0)
	DECLARE @TIPO		VARCHAR(1)
	DECLARE @UBICACION	NUMERIC(20,0)
	DECLARE @NAVE		NUMERIC(20,0)

	SET @CAMBIO_OK='0'

	-----------------------------------------------------------------------------------------
	--EXISTENCIA PALLET.
	-----------------------------------------------------------------------------------------
	SELECT	@CONT=COUNT(RL.RL_ID)
	FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
			ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
			INNER JOIN RL_DET_DOC_TRANS_POSICION RL
			ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
	WHERE	DD.PROP1=@PALLET_DESTINO

	IF @CONT=0 BEGIN
		RAISERROR('EL PALLET INGRESADO NO EXISTE.',16,1)
		RETURN
	END ELSE BEGIN
		SET @CONT=NULL
	END

	-----------------------------------------------------------------------------------------
	--MATERIAL EN EGRESO.
	-----------------------------------------------------------------------------------------
	SELECT	@CONT=COUNT(RL.RL_ID)
	FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
			ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
			INNER JOIN RL_DET_DOC_TRANS_POSICION RL
			ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
	WHERE	DD.PROP1=@PALLET_DESTINO
			AND RL.DOC_TRANS_ID_EGR IS NOT NULL

	IF @CONT>0 BEGIN
		RAISERROR('EL PALLET ACTUALMENTE SE ENCUENTRA EN PROCESO DE EGRESO.',16,1)
		RETURN
	END ELSE BEGIN
		SET @CONT=NULL
	END


	-----------------------------------------------------------------------------------------
	--POSICION PICKING PALLET
	-----------------------------------------------------------------------------------------
	SET @TIPO='1'

	SELECT	@NAVE=RL.NAVE_ACTUAL, @UBICACION=RL.POSICION_ACTUAL
	FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
			ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
			INNER JOIN RL_DET_DOC_TRANS_POSICION RL
			ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
	WHERE	DD.PROP1=@PALLET_DESTINO

	IF @UBICACION IS NOT NULL BEGIN

		SELECT	@CONT=COUNT(P.POSICION_ID)
		FROM	POSICION P
		WHERE	POSICION_ID=@UBICACION
				AND P.PICKING='1'

		IF @CONT=0 BEGIN
			RAISERROR('EL PALLET SELECCIONADO ESTA EN UNA UBICACION NO PICKEABLE.',16,1)
			RETURN
		END
	END ELSE BEGIN

		SELECT	@CONT=COUNT(NAVE_ID)
		FROM	NAVE N
		WHERE	NAVE_ID=@NAVE
				AND N.PICKING='1'

		IF @CONT=0 BEGIN
			RAISERROR('EL PALLET SELECCIONADO ESTA EN UNA UBICACION NO PICKEABLE.',16,1)
			RETURN
		END
	END

	-----------------------------------------------------------------------------------------
	--CATEGORIA LOGICA.
	-----------------------------------------------------------------------------------------
	SELECT	@CONT=COUNT(RL.RL_ID)
	FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
			ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
			INNER JOIN RL_DET_DOC_TRANS_POSICION RL
			ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
			INNER JOIN CATEGORIA_LOGICA CL 
			ON(RL.CAT_LOG_ID=CL.CAT_LOG_ID AND CL.DISP_EGRESO ='1' AND CL.PICKING='1')
	WHERE	DD.PROP1=@PALLET_DESTINO

	IF @CONT=0 BEGIN
		RAISERROR('EL PALLET SELECCIONADO TIENE UN ESTADO LOGICO NO EGRESABLE.',16,1)
		RETURN
	END

	SELECT	@CONT=COUNT(RL.RL_ID)
	FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
			ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
			INNER JOIN RL_DET_DOC_TRANS_POSICION RL
			ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
	WHERE	DD.PROP1=@PALLET_DESTINO

	SELECT	@CONT=COUNT(*)
	FROM	(	SELECT	DD.CLIENTE_ID							CLIENTE_ID, 
						DD.PRODUCTO_ID							PRODUCTO_ID, 
						ISNULL(DD.NRO_LOTE,'#B404AE')			NRO_LOTE,
						ISNULL(DD.FECHA_VENCIMIENTO,'19000101')	FECHA_VENCIMIENTO,
						ISNULL(DD.NRO_PARTIDA,'#B404AE')		NRO_PARTIDA,
						ISNULL(RL.EST_MERC_ID,'#B404AE')		EST_MERC_ID,
						DD.CAT_LOG_ID_FINAL
				FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
						ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
						INNER JOIN RL_DET_DOC_TRANS_POSICION RL
						ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID_EGR AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS_EGR)
						INNER JOIN DOCUMENTO D
						ON(DD.DOCUMENTO_ID=D.DOCUMENTO_ID)
				WHERE	DD.PROP1=@PALLET_ORIGEN
						AND D.TIPO_OPERACION_ID='EGR'
				UNION
				SELECT	DD.CLIENTE_ID							CLIENTE_ID, 
						DD.PRODUCTO_ID							PRODUCTO_ID, 
						ISNULL(DD.NRO_LOTE,'#B404AE')			NRO_LOTE,
						ISNULL(DD.FECHA_VENCIMIENTO,'19000101')	FECHA_VENCIMIENTO,
						ISNULL(DD.NRO_PARTIDA,'#B404AE')		NRO_PARTIDA,
						ISNULL(RL.EST_MERC_ID,'#B404AE')		EST_MERC_ID,
						RL.CAT_LOG_ID							CAT_LOG_ID
				FROM	DET_DOCUMENTO DD INNER JOIN DET_DOCUMENTO_TRANSACCION DDT
						ON(DD.DOCUMENTO_ID=DDT.DOCUMENTO_ID AND DD.NRO_LINEA=DDT.NRO_LINEA_DOC)
						INNER JOIN RL_DET_DOC_TRANS_POSICION RL
						ON(DDT.DOC_TRANS_ID=RL.DOC_TRANS_ID AND DDT.NRO_LINEA_TRANS=RL.NRO_LINEA_TRANS)
				WHERE	DD.PROP1=@PALLET_DESTINO)AS X

	IF @CONT=1 BEGIN
		SET @CAMBIO_OK='1'
	END ELSE BEGIN
		SET @CAMBIO_OK='0'
	END

END