
/****** Object:  StoredProcedure [dbo].[Frontera_PickingDistribucion]    Script Date: 02/04/2014 16:02:40 ******/
IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[Frontera_PickingDistribucion]') AND type in (N'P', N'PC'))
DROP PROCEDURE [dbo].[Frontera_PickingDistribucion]
GO

/****** Object:  StoredProcedure [dbo].[Frontera_PickingDistribucion]    Script Date: 02/04/2014 16:02:40 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE      PROCEDURE [dbo].[Frontera_PickingDistribucion]
AS

BEGIN
	DECLARE @ROLID		AS VARCHAR(5)
	DECLARE @USUARIOID	AS VARCHAR(30)
	
	SELECT	@ROLID=ROL_ID, 
			@USUARIOID=USUARIO_ID 
	FROM	#TEMP_USUARIO_LOGGIN

	SELECT 	U.NRO_GUIA								AS [GUIA]
			,U.NRO_HOJACARGA						AS [HOJA DE CARGA]
			,COUNT(DISTINCT P.DOCUMENTO_ID)			AS [CANT. DOCUMENTOS]
			,COUNT(DISTINCT P.NRO_UCEMPAQUETADO)	AS [CANT.CONTENEDORAS]
			,U.DOCK_ID								AS [DOCK]
			,T.NOMBRE								AS [TRANSPORTE]
			,CASE	WHEN X.CANT_CLIENTE > 1 
					THEN 'MULTICLIENTE' 
					ELSE C.RAZON_SOCIAL END			AS RAZON_SOCIAL

	FROM	DOCUMENTO D (NOLOCK)
			INNER JOIN DET_DOCUMENTO DD (NOLOCK) ON (D.DOCUMENTO_ID=DD.DOCUMENTO_ID)
			INNER JOIN PICKING P (NOLOCK) ON (DD.DOCUMENTO_ID=P.DOCUMENTO_ID AND DD.NRO_LINEA=P.NRO_LINEA)
			INNER JOIN RL_SYS_CLIENTE_USUARIO SU ON(D.CLIENTE_ID=SU.CLIENTE_ID)
			INNER JOIN CLIENTE C ON(D.CLIENTE_ID=C.CLIENTE_ID)
			INNER JOIN CLIENTE_PARAMETROS CP ON(C.CLIENTE_ID=CP.CLIENTE_ID)
			INNER JOIN UC_EMPAQUE U ON (P.NRO_UCEMPAQUETADO = U.UC_EMPAQUE)			
			INNER JOIN(	SELECT	CODIGO_VIAJE,COUNT(DISTINCT SD.CLIENTE_ID) AS CANT_CLIENTE    
						FROM	SYS_INT_DOCUMENTO SD    
						--WHERE	SD.ESTADO_GT IS NULL  
						GROUP BY SD.CODIGO_VIAJE    
			) AS X ON (X.CODIGO_VIAJE=D.NRO_DESPACHO_IMPORTACION)	
			LEFT JOIN TRANSPORTE T ON (U.TRANSPORTE_ID = T.TRANSPORTE_ID)			
	WHERE	P.FIN_PICKING IN ('2')
			AND P.FACTURADO='0'
			AND SU.USUARIO_ID=@USUARIOID
			AND P.ST_CAMION='0'
			AND P.CANT_CONFIRMADA>0
			AND DBO.GET_TIPO_DOCUMENTO_ID(D.CLIENTE_ID,D.NRO_REMITO) IN (SELECT R.TIPO_DOCUMENTO_ID FROM RL_ROL_INT_TIPO_DOCUMENTO R WHERE R.ROL_ID=@ROLID)
			--PARA PODER DETERMINAR SI VA POR DESCONSOLIDACION OBLIGATORIA.
			AND ((ISNULL(CP.FLG_DESCONSOLIDACION,'0')='0')OR(ISNULL(P.ESTADO,'0') IN('2')))
			--PARA PODER DETERMINAR SI VA POR EMPAQUETADO OBLIGATORIO
			AND ((ISNULL(CP.FLG_EMPAQUETADO,'0')='0')OR(P.NRO_UCEMPAQUETADO IS NOT NULL))	
			AND ISNULL(CP.FLG_DISTRIBUCION,'0')='1'		
	GROUP BY 
			U.NRO_GUIA,U.NRO_HOJACARGA,U.DOCK_ID,T.NOMBRE,CASE WHEN X.CANT_CLIENTE > 1 THEN 'MULTICLIENTE' ELSE C.RAZON_SOCIAL END

END




GO


